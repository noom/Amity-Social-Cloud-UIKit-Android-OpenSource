subprojects {
    if (it.name == "amity-sample-code")
        return

    apply plugin: "maven-publish"

    plugins.withId('com.android.library') {
        android {
            publishing {
                singleVariant('release') {
                    withSourcesJar()
                }
            }
        }

        afterEvaluate {
            publishing {
                repositories {
                    maven {
                        name = "AmityFork"
                        url = uri("https://maven.pkg.github.com/noom/Amity-Social-Cloud-UIKit-Android-OpenSource")
                        credentials {
                            username = System.getenv("NOOM_GITHUB_PACKAGES_USERNAME")
                                    ?: System.getenv("GITHUB_PACKAGES_USERNAME")

                            password = System.getenv("NOOM_GITHUB_PACKAGES_TOKEN")
                                    ?: System.getenv("GITHUB_PACKAGES_TOKEN")
                        }
                    }
                }

                publications {
                    release(MavenPublication) {
                        artifactId = "$project.name"
                        // Applies the component for the release build variant
                        from components.release
                    }
                }
            }
        }
    }
}
