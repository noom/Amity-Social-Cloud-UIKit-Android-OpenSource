apply from: "../buildsystem/activity.gradle"
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply from: "../publish.gradle"

android {
    namespace 'com.amity.socialcloud.uikit.chat.compose'

    buildFeatures {
        dataBinding true
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.7"
    }

    packagingOptions {
        pickFirst("META-INF/*.kotlin_module")
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/io.netty.versions.properties'
    }

    publishing {
        singleVariant('release')
    }
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "AmityFork"
                url = uri("https://maven.pkg.github.com/noom/Amity-Social-Cloud-UIKit-Android-OpenSource")
                credentials {
                    username = System.getenv("NOOM_GITHUB_PACKAGES_USERNAME")
                            ?: System.getenv("GITHUB_PACKAGES_USERNAME")

                    password = System.getenv("NOOM_GITHUB_PACKAGES_TOKEN")
                            ?: System.getenv("GITHUB_PACKAGES_TOKEN")
                }
            }
        }

        publications {
            release(MavenPublication) {
                artifactId = "$project.name"
                // Applies the component for the release build variant
                from components.release
                // Adds javadocs and sources as separate jars.
                artifact androidSourcesJar
            }
        }
    }
}

dependencies {

    api project(path: ':common')
    api project(path: ':common-compose')

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
