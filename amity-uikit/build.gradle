apply from: "../buildsystem/activity.gradle"
apply plugin: 'com.android.library'
apply from: "../publish.gradle"

android {
    namespace 'com.amity.socialcloud.uikit'
    buildTypes {
        release {
            consumerProguardFiles 'amity-proguard-rules.pro'
        }
        debug {
            consumerProguardFiles 'amity-proguard-rules.pro'
        }
    }
    packagingOptions {
        pickFirst("META-INF/*.kotlin_module")
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/io.netty.versions.properties'
    }

    publishing {
        singleVariant('release')
    }
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "AmityFork"
                url = uri("https://maven.pkg.github.com/noom/Amity-Social-Cloud-UIKit-Android-OpenSource")
                credentials {
                    username = System.getenv("NOOM_GITHUB_PACKAGES_USERNAME")
                            ?: System.getenv("GITHUB_PACKAGES_USERNAME")

                    password = System.getenv("NOOM_GITHUB_PACKAGES_TOKEN")
                            ?: System.getenv("GITHUB_PACKAGES_TOKEN")
                }
            }
        }

        publications {
            release(MavenPublication) {
                artifactId = "$project.name"
                // Applies the component for the release build variant
                from components.release
                // Adds javadocs and sources as separate jars.
                artifact androidSourcesJar
            }
        }
    }
}

dependencies {
    api project(path: ':social')
    api project(path: ':chat')

    api "com.github.AmityCo.Amity-Social-Cloud-SDK-Android:amity-sdk:$amityMessagingSdkVersion"
}
